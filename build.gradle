plugins {
    id 'java'
    id 'application'
    id 'org.openjfx.javafxplugin' version '0.0.13'
    id 'org.beryx.jlink' version '2.25.0'
}

group 'io.github.kamilszewc'
version '1.4'

repositories {
    mavenCentral()
}

ext {
    junitVersion = '5.9.1'
}

sourceCompatibility = '18'
targetCompatibility = '18'

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
}

application {
    mainModule = 'io.github.kamilszewc.twofactorclient'
    mainClass = 'io.github.kamilszewc.twofactorclient.Application'
}

javafx {
    version = '19'
    modules = ['javafx.controls', 'javafx.fxml']
}

dependencies {

    testImplementation("org.junit.jupiter:junit-jupiter-api:${junitVersion}")
    testRuntimeOnly("org.junit.jupiter:junit-jupiter-engine:${junitVersion}")

    // Lombok
    compileOnly 'org.projectlombok:lombok:1.18.24'
    annotationProcessor 'org.projectlombok:lombok:1.18.24'

    // Totp generator
    implementation 'io.github.kamilszewc:totp:1.3'
}

test {
    useJUnitPlatform()
}

import org.apache.tools.ant.taskdefs.condition.Os;

jlink {
    imageZip = project.file("${buildDir}/distributions/app-${javafx.platform.classifier}.zip")
    options = ['--strip-debug', '--compress', '2', '--no-header-files', '--no-man-pages']
    launcher {
        name = 'app'
    }

    jpackage {
        outputDir = "packages"
        imageName = "Two Factor Client"
        skipInstaller = false
        installerName = "two-factor-client"
        vendor = "Kamil Szewc"
        if (Os.isFamily(Os.FAMILY_MAC)) {
            installerType = 'pkg'
            icon = 'src/main/resources/icon.icns'
        } else if (Os.isFamily(Os.FAMILY_WINDOWS)) {
            installerType = 'msi'
            icon = 'src/main/resources/logo.png'
        } else {
            installerType = 'deb'
            icon = 'src/main/resources/logo.png'
        }
    }
}

jlinkZip {
    group = 'distribution'
}

def copyLicense = tasks.register('copyLicense', Copy) {
    from 'LICENSE'
    into 'src/main/resources'
}

compileJava.configure {
    dependsOn copyLicense
}
